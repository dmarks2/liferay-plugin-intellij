//Poshi Script documentation: https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-script.markdown
{
   parserClass="de.dm.intellij.liferay.language.poshi.parser.PoshiParser"

   extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

   psiClassPrefix="Poshi"
   psiImplClassSuffix="Impl"
   psiPackage="de.dm.intellij.liferay.language.poshi.psi"
   psiImplPackage="de.dm.intellij.liferay.language.poshi.psi.impl"

   elementTypeHolderClass="de.dm.intellij.liferay.language.poshi.psi.PoshiTypes"
   elementTypeClass="de.dm.intellij.liferay.language.poshi.psi.PoshiElementType"
   tokenTypeClass="de.dm.intellij.liferay.language.poshi.psi.PoshiTokenType"

   tokens = [
        //keywords
        DEFINITION = "definition"
        SET_UP = "setUp"
        TEAR_DOWN = "tearDown"
        STATIC = "static"
        VAR = "var"
        PROPERTY = "property"
        FUNCTION = "function"
        MACRO = "macro"
        TEST = "test"
        IF = "if"
        ELSE_IF = "else if"
        ELSE = "else"
        TASK = "task"
        WHILE = "while"
        RETURN = "return"

        //declarations
        ANNOTATION_NAME = "regexp:(@[\s]*[a-z0-9]+)"
        IDENTIFIER = "regexp:([A-z][\w-]*)+"
        VARIABLE_REFERENCE = "regexp:(\$\{[\w-]+\})"

        //operators and other tokens
        CURLY_LBRACE = "{"
        CURLY_RBRACE = "}"
        ROUND_LBRACE = "("
        ROUND_RBRACE = ")"
        SQUARE_LBRACE = "["
        SQUARE_RBRACE = "]"
        PERIOD = "."
        SEMICOLON = ";"
        EQUALS = "="
        DOUBLE_QUOTE = '"'
        COMMA = ","

        BLOCK_COMMENT = "regexp:/\*.*?\*/"
        LINE_COMMENT = "regexp://.*"

        DOUBLE_QUOTED_STRING = "regexp:(\"(.*?)\")"
        SINGLE_QUOTED_STRING = "regexp:('(.*?)')"

        COMPARISION_OPERATOR = "regexp:[!=]="
        ARITHMETIC_OPERATOR = "regexp:(\-|\+|\*/)"
        NUMERIC_CONSTANT = "regexp:[0-9]+"

        TESTING_WHITESPACE = "regexp:[ \n\t\r\f]+"

    ]
}

// Root

poshi-file ::= { poshi-code* }

// Identifiers

private poshi-code ::= { annotations | command-blocks | comments | control-blocks | invocations | keywords | properties | structures | variables}

annotations ::= (ANNOTATION_NAME [EQUALS string-quoted-double])  //TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L29

command-blocks ::= { (FUNCTION | MACRO | TEST) IDENTIFIER CURLY_LBRACE [poshi-code*] CURLY_RBRACE}

comment-block ::= BLOCK_COMMENT

comments ::= comment-block | LINE_COMMENT

control-blocks ::= (control-block-with-condition | control-block-without-condition) CURLY_LBRACE [poshi-code*] CURLY_RBRACE //TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L104

private control-block-with-condition ::= (IF | ELSE_IF | TASK | WHILE) ROUND_LBRACE control-block-inner* ROUND_RBRACE

private control-block-without-condition ::= (ELSE)

private control-block-inner ::= invocations | string-quoted-double | variable-assignment | COMPARISION_OPERATOR

//TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L110

invocations ::= IDENTIFIER [PERIOD IDENTIFIER] ROUND_LBRACE [invocation-inner-list] ROUND_RBRACE

private invocation-inner-list ::= invocation-inner (COMMA invocation-inner)* {pin(".*")=0}

private invocation-inner ::= [variable-assignment | IDENTIFIER | string-quoted-double]

keywords ::= (SEMICOLON | COMPARISION_OPERATOR | RETURN)

//TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L172

//TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L188

properties ::= PROPERTY IDENTIFIER EQUALS (string-quoted-double)

string-quoted-double ::= DOUBLE_QUOTED_STRING  //TODO not applied, required? https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L332

//TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L400

//TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L420

structures ::= structure-keywords CURLY_LBRACE [poshi-code*] CURLY_RBRACE

structure-keywords ::= DEFINITION | SET_UP | TEAR_DOWN

variable-assignment ::= IDENTIFIER EQUALS (invocations | string-quoted-double | VARIABLE_REFERENCE | ARITHMETIC_OPERATOR | NUMERIC_CONSTANT) //TODO https://github.com/liferay/liferay-portal/blob/master/modules/test/poshi/poshi-vscode/syntaxes/poshi.tmLanguage.json#L463

variables ::= [STATIC] VAR variable-assignment
